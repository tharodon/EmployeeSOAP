plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.1'
	id "com.diffplug.spotless" version "6.7.2"
}

bootJar {
	mainClass = 'com.example.employeesoap.EmployeeSoapApplication'
}

spotless {
	format 'misc', {
		target '*.gradle', '*.md', '.gitignore', '*.java'

		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		custom 'noWildcardImports', {
			def wildcard = it =~ /(import.*\*;)/
			def packageName = it =~ /(package.*;)/
			def string = String.valueOf(it)
			if (packageName.find()) {
				print(
						'no use wildcard in '
						+ string.subSequence(packageName.start(), packageName.end())
						+ ' '
				)
			}
			if (wildcard.find()) {
				println(string.subSequence(wildcard.start(), wildcard.end()))
			}
		}
		bumpThisNumberIfACustomStepChanges(1)
		googleJavaFormat('1.8').aosp().reflowLongStrings()
		removeUnusedImports()
		importOrder()
	}
}

repositories {
	mavenCentral()

}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.7'
	implementation 'org.springframework.boot:spring-boot-starter-security:2.6.7'
	implementation 'org.springframework.boot:spring-boot-starter-web:2.6.7'
	implementation 'com.github.librepdf:openpdf:1.3.28'
	implementation 'io.swagger.core.v3:swagger-annotations:2.1.6'
	implementation 'org.springdoc:springdoc-openapi-ui:1.5.2'
	implementation 'org.springframework.kafka:spring-kafka:2.8.5'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
	implementation 'org.flywaydb:flyway-core:8.5.12'
	runtimeOnly 'org.postgresql:postgresql:42.3.4'
	testImplementation 'org.testcontainers:postgresql:1.17.2'
	testImplementation 'org.testcontainers:kafka:1.17.2'
	testImplementation 'org.springframework.security:spring-security-test:4.2.2.RELEASE'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.7'
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor "org.projectlombok:lombok:1.18.24"
	testImplementation 'org.projectlombok:lombok:1.18.24'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'EmployeeSOAP'
java.sourceCompatibility = JavaVersion.VERSION_11


test {
	useJUnitPlatform()
}
